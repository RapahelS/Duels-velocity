import org.apache.tools.ant.filters.ReplaceTokens

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        include '**/*.json'
        filter(ReplaceTokens, tokens: [VERSION: project.version])
    }
}

tasks.withType(Jar).configureEach {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

dependencies {
    compileOnly 'org.jetbrains:annotations-java5:24.1.0'
    compileOnly 'org.projectlombok:lombok:1.18.38'
    annotationProcessor 'org.projectlombok:lombok:1.18.38'
    compileOnly 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT'
    annotationProcessor 'com.velocitypowered:velocity-api:3.3.0-SNAPSHOT'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.19.2'
    implementation 'org.json:json:20250517'
    implementation 'com.zaxxer:HikariCP:5.1.0'
    implementation 'com.mysql:mysql-connector-j:9.2.0'
    implementation project(':duels-common')
}

shadowJar {
    getDestinationDirectory().set(file("$rootDir/out/"))

    final String archiveName = project.name + '-' + project.version + '.jar'
    getArchiveFileName().set(archiveName)

    dependencies {
        include(project(':duels-common'))
        include(dependency('com.fasterxml.jackson.core:.*'))
        include(dependency('org.json:.*'))
        include(dependency('com.zaxxer:HikariCP:.*'))
        include(dependency('com.mysql:.*'))
    }

    mergeServiceFiles {
        include 'META-INF/services/java.sql.Driver'
    }

    final String group = project.group.toString() + "." + parent.name.toLowerCase() + ".shaded."
    relocate 'com.fasterxml.jackson', 'com.meteordevelopments.duels.shaded.jackson'
    relocate 'com.zaxxer.hikari', group + 'hikari'
}

// To build Duels velocity plugin jar, run './gradlew :duels-velocity:clean :duels-velocity:build'.
build {
    dependsOn(shadowJar)
}